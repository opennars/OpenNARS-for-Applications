*motorbabbling=false
*volume=0

//to open the entry door you need to use a knob
<(<{#1} --> knob> &/ <({SELF} * {#1}) --> ^pick>) =/> <door --> [open]>>.
//once door is open, you can go to the corridor
<(<door --> [open]> &/ <({SELF} * corridor) --> ^go>) =/> <clock --> [large]>>.
//from the corridor you can go into a classroom with either clean or dirty blackboard
<(<clock --> [large]> &/ <({SELF} * classrom) --> ^go>) =/> <blackboard --> [clean]>>.
<(<clock --> [large]> &/ <({SELF} * classrom) --> ^go>) =/> <blackboard --> [dirty]>>.

//presenting the content using a clean blackboard will educate the children
<(<blackboard --> [clean]> &/ <({SELF} * write) --> ^pick>) =/> <child --> [educated]>>.
//but if the blackboard is dirty, it needs to be cleaned
<(<blackboard --> [dirty]> &/ <({SELF} * sponge) --> ^pick>) =/> <child --> [educated]>>.
//you can educate children in the classroom
<<blackboard --> [#1]> =/> <child --> [educated]>>.

//once blackboard is green one can go to corridor
<(<blackboard --> [clean]> &/ <({SELF} * corridor) --> ^go>) =/> <clock --> [large]>>.
//from the corridor you can go into the kitchen with microwave seen
<(<clock --> [large]> &/ <({SELF} * kitchen) --> ^go>) =/> <microwave --> [seen]>>.

//if there is a microwave there is coffee
<<microwave --> [seen]> =/> <{SELF} --> [refreshed]>>.
//whatever coffee you can find, use it for refresh
<(<{#1} --> coffee> &/ <({SELF} * {#1}) --> ^pick>) =/> <{SELF} --> [refreshed]>>.

//coffee is black
<coffee --> [black]>.
//knobs are on door
<(knob * door) --> on>.

4
//obj1 is on the door
<({obj1} * door) --> on>. :|:
//and an object on a window
<({obj2} * window) --> on>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
4

//expected: ^pick executed with args ({SELF} * {obj1})
//you are in a place with a large clock now (the corridor!)
<door --> [open]>. :|:
<clock --> [large]>. :|:
//with a red floor
<floor --> [red]>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
4
//expected: ^go executed with args ({SELF} * classrom)
//you are somewhere with a clean blackboard (you are in the classroom!)
<blackboard --> [clean]>. :|:
//and a blue floor as well
<floor --> [blue]>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
4
//expected: ^pick executed with args ({SELF} * write)
//your are at a place with a blackboard which now is dirty (blackboard was used!)
<blackboard --> [dirty]>. :|:
//the floor is still blue
<floor --> [blue]>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
4
//expected: ^pick executed with args ({SELF} * sponge)
//you are at a place with a clean blackboard now (still in the classroom!)
<blackboard --> [clean]>. :|:
//with a blue floor still
<floor --> [blue]>. :|:
//you are really tired now, you want to be refreshed (go for coffee!!)
<{SELF} --> [refreshed]>! :|:
4

//expected: ^go executed with args ({SELF} * corridor)
//now you are in the place with a large clock (the corridor!)
<clock --> [large]>. :|:
//with a red floor
<floor --> [red]>. :|:
//you are really tired, you want to be refreshed (go for coffee!!)
<{SELF} --> [refreshed]>! :|:

4

//expected: ^go executed with args ({SELF} * kitchen)
//now you are at a place with a microwave which is on
<microwave --> [on]>. :|:
//with a cyan floor
<floor --> [cyan]>. :|:
//and see a black drink
<{drink1} --> [black]>. :|:
//and a white drink
<{drink2} --> [white]>. :|:
//you are really tired, you want to be refreshed (go for coffee!!)
<{SELF} --> [refreshed]>! :|:
//expected: ^pick executed with args ({SELF} * {drink1})

//Q&A time
//What color does the floor have where the large clock is?
<<clock --> [large]> =/> <floor --> [?what]>>?
//expected: Answer: <<clock --> [large]> =/> <floor --> [red]>>. Truth: frequency=0.909098, confidence=0.463819
