*motorbabbling=false
*volume=0
//to open the entry door you need to use a knob
<(<#1 --> knob> &/ <({SELF} * #1) --> ^pick>) =/> <door --> [open]>>.
//a knob is a metallic object attached to a door
<knob --> ([metallic] & (on /1 door))>.
//well sometimes it's a plastic object attached to a door
<knob --> ([plastic] & (on /1 door))>.
//once door is open, you can go to the corridor
<(<door --> [open]> &/ <({SELF} * corridor) --> ^go>) =/> <corridor --> [in]>>.
//from the corridor you can go into every room
<(<corridor --> [in]> &/ <({SELF} * $1) --> ^go>) =/> <$1 --> [in]>>.
//from every room you can go to the corridor
<(<#1 --> [in]> &/ <({SELF} * corridor) --> ^go>) =/> <corridor --> [in]>>.
//from the kitchen you can get something to get refreshed
<<kitchen --> [in]> =/> <{SELF} --> [refreshed]>>.
//whatever coffee you can find, use it for refresh
<(<#1 --> coffee> &/ <({SELF} * #1) --> ^pick>) =/> <{SELF} --> [refreshed]>>.
//you can educate children in the classroom
<<classroom --> [in]> =/> <child --> [educated]>>.
//classrooms contain blackboards
<<blackboard --> [#1]> =/> <classroom --> [in]>>.
//kitchens contain microwaves
<<microwave --> [#1]> =/> <kitchen --> [in]>>.
//school corridors contain a large clock
<<clock --> [large]> =/> <corridor --> [in]>>.
//presenting the content using a clean blackboard will educate the children
<(<blackboard --> [clean]> &/ <({SELF} * write) --> ^pick>) =/> <child --> [educated]>>.
//but if the blackboard is dirty, it needs to be cleaned
<(<blackboard --> [dirty]> &/ <({SELF} * sponge) --> ^pick>) =/> <child --> [educated]>>.
//coffee is black
<coffee --> [black]>.
//orange juice is yellow
<orangejuice --> [yellow]>.
//milk is white
<milk --> [white]>.
15
//arriving at school, you see a wooden object (a door knob!)
<{obj1} --> [wooden]>. :|:
//on the door
<{obj1} --> (on /1 door)>. :|:
//and a wooden object
<{obj2} --> [wooden]>. :|:
//on a window
<{obj2} --> (on /1 window)>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
15
//expected: ^pick executed with args ({SELF} * {obj1})
//you are in a place with a large clock now (the corridor!)
<clock --> [large]>. :|:
//with a red floor
<floor --> [red]>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
15
//expected: ^go executed with args ({SELF} * classroom)
//you are somewhere with a clean blackboard (you are in the classroom!)
<blackboard --> [clean]>. :|:
//and a blue floor as well
<floor --> [blue]>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
15
//expected: ^pick executed with args ({SELF} * write)
//your are at a place with a blackboard which now is dirty (blackboard was used!)
<blackboard --> [dirty]>. :|:
//the floor is still blue
<floor --> [blue]>. :|:
//the goal is to educate children
<child --> [educated]>! :|:
15
//expected: ^pick executed with args ({SELF} * sponge)
//you are at a place with a clean blackboard now (still in the classroom!)
<blackboard --> [clean]>. :|:
//with a blue floor still
<floor --> [blue]>. :|:
//you are really tired now, you want to be refreshed (go for coffee!!)
<{SELF} --> [refreshed]>! :|:
15
//expected: ^go executed with args ({SELF} * corridor)
//now you are in the place with a large clock (the corridor!)
<clock --> [large]>. :|:
//with a red floor
<floor --> [red]>. :|:
//you are really tired, you want to be refreshed (go for coffee!!)
<{SELF} --> [refreshed]>! :|:
15
//expected: ^go executed with args ({SELF} * kitchen)
//now you are at a place with a microwave which is on
<microwave --> [on]>. :|:
//with a cyan floor
<floor --> [cyan]>. :|:
//and see a black drink
<{drink1} --> [black]>. :|:
//and a white drink
<{drink2} --> [white]>. :|:
//you are really tired, you want to be refreshed (go for coffee!!)
<{SELF} --> [refreshed]>! :|:
15
//expected: ^pick executed with args ({SELF} * {drink1})

//Q&A time
//What color does the floor have where the large clock is?
<<clock --> [large]> =/> <floor --> [?what]>>?
//expected: Answer: <<clock --> [large]> =/> <floor --> [red]>>. Truth: frequency=0.964794, confidence=0.449068
//If you are where the floor is red, where do you need to go to see a cyan floor?
<(<floor --> [red]> &/ <({SELF} * ?where) --> ^go>) =/> <floor --> [cyan]>>?
//expected: Answer: <(<floor --> [red]> &/ <({SELF} * kitchen) --> ^go>) =/> <floor --> [cyan]>>. Truth: frequency=1.000000, confidence=0.01029
5000
