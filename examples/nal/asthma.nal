*volume=0
//Stressed people tend to smoke
<<$1 --> [stressed]> ==> <$1 --> [smokes]>>. {1.0 0.99}
//A smoker befriended with someone else often makes the other person smoke as well
<<#1 --> ((friend /2 $2) & [smokes])> ==> <$2 --> [smokes]>>. {1.0 0.9}
//If someone smokes, he gets asthma
<<$1 --> [smokes]> ==> <$1 --> [asthma]>>. {1.0 0.9}
100
//Angelika smokes
<{angelika} --> [smokes]>.
//Angelika is a friend of Jonas
<{angelika} --> (friend /1 {jonas})>.
//Jonas is a friend of Angelika
<{jonas} --> (friend /1 {angelika})>.
100
//Jonas has asthma?
<{jonas} --> [asthma]>?
//expected: Answer: <{jonas} --> [asthma]>. creationTime=190 Truth: frequency=1.000000, confidence=0.531441
//Angelika has asthma?
<{angelika} --> [asthma]>?
//expected: Answer: <{angelika} --> [asthma]>. creationTime=176 Truth: frequency=1.000000, confidence=0.810000
100
//Jonas is stressed
<{jonas} --> [stressed]>.
100
//Jonas has asthma?
<{jonas} --> [asthma]>?
//expected: Answer: <{jonas} --> [asthma]>. creationTime=379 Truth: frequency=1.000000, confidence=0.815224
//Angelika has asthma?
<{angelika} --> [asthma]>?
//expected: Answer: <{angelika} --> [asthma]>. creationTime=224 Truth: frequency=1.000000, confidence=0.810000
